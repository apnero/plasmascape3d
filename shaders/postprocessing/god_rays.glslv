#define LIGHT_INDEX 0

attribute vec2 a_bb_vertex;

uniform mat4 u_view_matrix;
uniform mat4 u_proj_matrix;

uniform vec3 u_sun_direction;

#if DEPTH_RGBA
    uniform vec3 u_camera_eye;
    #if WATER_EFFECTS
        uniform vec4 u_camera_quat;
        uniform vec4 u_refl_plane;
        varying float v_underwater;
        varying vec2 v_texture_offset;
    #endif
#endif

varying vec2 v_texcoord;
varying vec4 v_sun_pos_clip;

void multiply_vec3 (in vec4 quat, in vec3 vec, out vec3 dest) {

    // calculate quat * vec
    float ix = quat.w * vec.x + quat.y * vec.z - quat.z * vec.y;
    float iy = quat.w * vec.y + quat.z * vec.x - quat.x * vec.z;
    float iz = quat.w * vec.z + quat.x * vec.y - quat.y * vec.x;
    float iw = -quat.x * vec.x - quat.y * vec.y - quat.z * vec.z;

    // calculate result * inverse quat
    dest.x = ix * quat.w - iw * quat.x - iy * quat.z + iz * quat.y;
    dest.y = iy * quat.w - iw * quat.y - iz * quat.x + ix * quat.z;
    dest.z = iz * quat.w - iw * quat.z - ix * quat.y + iy * quat.x;

}

void main(void) {
    v_texcoord = a_bb_vertex + 0.5;
    
    vec3 dir = normalize(u_sun_direction);

    // locate sun center
    v_sun_pos_clip = u_proj_matrix * u_view_matrix * vec4(dir, 0.0);
 
    #if DEPTH_RGBA && WATER_EFFECTS
        if (u_camera_eye.y + a_bb_vertex.t < - u_refl_plane.w) {

            v_underwater = 1.0;


            vec3 y_axis = vec3 (0.0, 1.0, 0.0);
            vec3 cam_y_dir;
            multiply_vec3(u_camera_quat, y_axis, cam_y_dir);
            cam_y_dir = normalize(cam_y_dir);

            //angle cos between camera local y and global y
            float cos_camz_y = dot(cam_y_dir, vec3(0.0, 1.0, 0.0));

            float atan2_xz = atan(cam_y_dir.x, cam_y_dir.z);

            //Offset generated by camera rotation
            v_texture_offset = vec2(-atan2_xz, acos(cos_camz_y));

        } else {
            v_underwater = 0.0;
        }
    #endif

    v_sun_pos_clip.x /= v_sun_pos_clip.w;
    v_sun_pos_clip.y /= v_sun_pos_clip.w;
    v_sun_pos_clip.x = 0.5 * (v_sun_pos_clip.x + 1.0);
    v_sun_pos_clip.y = 0.5 * (v_sun_pos_clip.y + 1.0);
    //Remove double god_rays
    v_sun_pos_clip += 99999.0 * step(v_sun_pos_clip.z, 0.0);

    gl_Position = vec4(2.0 * a_bb_vertex.xy, 0.0, 1.0);
}
